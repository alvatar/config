;; el-get
(add-to-list 'load-path "~/.emacs.d/el-get/el-get")
;(setq user-emacs-directory "~/.emacs.d")

(unless (require 'el-get nil t)
(url-retrieve
 "https://github.com/dimitri/el-get/raw/master/el-get-install.el"
 (lambda (s)
   (end-of-buffer)
   (eval-print-last-sexp))))

;; now either el-get is `require'd already, or have been `load'ed by the
;; el-get installer.

;; set local recipes, el-get-sources should only accept PLIST element
(setq
 el-get-sources
 '((:name buffer-move                   ; have to add your own keys
    :after (progn
             (global-set-key (kbd "<C-S-up>")     'buf-move-up)
             (global-set-key (kbd "<C-S-down>")   'buf-move-down)
             (global-set-key (kbd "<C-S-left>")   'buf-move-left)
             (global-set-key (kbd "<C-S-right>")  'buf-move-right)))
   (:name smex                          ; a better (ido like) M-x
    :after (progn
             (setq smex-save-file "~/.emacs.d/.smex-items")
             (global-set-key (kbd "M-x") 'smex)
             (global-set-key (kbd "M-X") 'smex-major-mode-commands)))
   (:name magit                        ; git meet emacs, and a binding
    :after (progn
             (global-set-key (kbd "C-x C-z") 'magit-status)))
   (:name switch-window          ; takes over C-x o
    :after (progn
             (global-set-key (kbd "C-x o") 'switch-window)))
   (:name goto-last-change          ; move pointer back to last change
          :after (progn
                   ;; when using AZERTY keyboard, consider C-x C-_
                   (global-set-key (kbd "C-x C-/") 'goto-last-change)))
   (:name paredit
    :after (progn
             (add-hook 'emacs-lisp-mode-hook (lambda () (paredit-mode +1)))
             (add-hook 'scheme-mode-hook (lambda () (paredit-mode +1)))
             (add-hook 'scheme-interaction-mode-hook (lambda () (paredit-mode +1)))))
   (:name linum-ex
    :after (progn
             (global-linum-mode 1)))
   (:name color-theme-solarized ; Color theme
    :after (progn
             (when (display-graphic-p)
               (color-theme-solarized 'light))))
   (:name js2-mode
    :after (progn
             (setq js-indent-level 2)))))

;; now set our own packages
(setq
 my:el-get-packages
 (append
  '(el-get                  ; el-get is self-hosting
    auto-complete           ; complete as you type with overlays
    emmet-mode              ; zencoding evolved
    color-theme             ; nice looking emacs
    iedit                   ; edit multiple occurrences simultaneously
    js2-mode
    )
  (mapcar 'el-get-source-name el-get-sources)))
   
;; install new packages and init already installed packages
(el-get 'sync my:el-get-packages)

;;-----------------

;; General Config

(setq backup-inhibited t)
(setq auto-save-default nil)
(setq-default indent-tabs-mode nil)
(setq make-backup-files nil)
(setq browse-url-generic-program (executable-find "firefox")
      browse-url-browser-function 'browse-url-generic)
(fset 'yes-or-no-p 'y-or-n-p)

;;; Look & feel

(custom-set-variables
 '(inhibit-startup-screen t)
 ;;'(initial-buffer-choice "/")
 '(load-home-init-file t t))
(custom-set-faces)
(menu-bar-mode 0)
(if (display-graphic-p)
    (progn
      (scroll-bar-mode 0)
      (tool-bar-mode 0)
      ;; Fonts
      (if (eq system-type 'darwin)
          (progn
            ;; default Latin font (e.g. Consolas)
            (set-face-attribute 'default nil :family "Courier")
            ;; default font size (point * 10)
            ;; WARNING!  Depending on the default font,
            ;; if the size is not supported very well, the frame will be clipped
            ;; so that the beginning of the buffer may not be visible correctly. 
            (set-face-attribute 'default nil :height 110))
        (progn
          (set-default-font "-*-montecarlo-medium-*-normal-*-*-*-*-*-*-*-*-*")
          (set-face-attribute 'default nil :height 110)
          (set-face-font 'font-lock-comment-face "-*-montecarlo-medium-*-normal-*-*-*-*-*-*-*-*-*")
          (set-face-foreground 'font-lock-comment-delimiter-face "DimGrey")
          (set-face-font 'font-lock-comment-delimiter-face "-*-montecarlo-medium-*-normal-*-*-*-*-*-*-*-*-*")
          (set-face-foreground 'font-lock-comment-face "DimGrey")))))
(column-number-mode 1)
(line-number-mode 1)
(blink-cursor-mode 0)
(global-font-lock-mode 1)
(transient-mark-mode 1)
(setq show-paren-delay 0
      show-paren-style 'parenthesis)
(show-paren-mode 1)

;; Aspell

(setq ispell-program-name "aspell")
(setq exec-path (cons "/usr/local/bin/" exec-path))

;; Scheme

(setq scheme-program-name "/usr/local/Gambit-C/bin/gsc")
(font-lock-add-keywords 'scheme-mode
                        '(("(\\(lambda\\)\\>" (0 (prog1 ()
                                                   (compose-region (match-beginning 1)
                                                                   (match-end 1)
                                                                   ?Î»))))))
(global-set-key "\C-c\C-qr" 'run-scheme)
(global-set-key "\C-c\C-c" 'comment-region)
(global-set-key "\C-c\M-c" 'uncomment-region)
(add-hook 'scheme-mode-hook
          (lambda ()
            (define-key scheme-mode-map "\C-c\C-i" 'scheme-import-file)))
(add-hook 'inferior-scheme-mode-hook
          (lambda ()
            (linum-mode 0)))
(add-hook 'inferior-scheme-mode-hook
          (lambda ()
            (define-key scheme-mode-map "\C-c\C-c" 'comment-region)
            (define-key scheme-mode-map "\C-c\M-c" 'uncomment-region)))

;; Load remote SchemeSpheres remote debugging if installed
(let ((sense-emacs "~/Dropbox/projects/sphere-energy/src/remote/sense-emacs.el"
       ;;"/usr/local/Gambit-C/spheres/energy/src/remote/sense-emacs.el"
       ))
  (message "Emacs Sense loaded")
  (if (file-exists-p sense-emacs)
      (load-file sense-emacs)))




;;;------------------------------------------------------------------------------

;;; swiss-move.el --- Swiss Cursor Movement
;; $Id: swiss-move.el,v 1.1.1.1 2004/04/01 15:56:47 ska Exp $
;; Copyright (C) 2004 by Stefan Kamphausen
;; Author: Stefan Kamphausen <http://www.skamphausen.de>
;; Keywords: tools, user
;; This file is not part of XEmacs.

;; This program is free software; you can redistribute it and/or modify it
;; under the terms of the GNU General Public License as published by
;; the Free Software Foundation; either version 2, or (at your option)
;; any later version.

;; This program is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this program; see the file COPYING. If not, write to the Free
;; Software Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
;; 02111-1307, USA.


;;; Commentary:
;; - Moving one line at a time is slow, but precise.
;; - Moving in screenfulls (aka scrolling) ist not fast enough for
;;   large buffers and is not precise.
;; Thus you ususally scroll to another part of your large file and
;; then use the one-line-movement for exact adjustment.
;;
;; This can be improved if subsequent keystrokes would reduce the
;; amount of lines scrolled.  Please see the docstring of
;; `swiss-move-line' for examples that try to explain it.
;; You'd probably best try it out now.
;;
;; Actually the term "Swiss Movement" is introduced here and it stems
;; from the *li-variable names that occur in these functions ;-)
;;
;; Installation:
;; ------------
;; Put
;; (require 'swiss-move)
;; somewhere in you init files and add the keybinding, e.g.:
;; (global-set-key '[(shift prior)]           'swiss-move-line-up)
;; (global-set-key '[(shift next)]            'swiss-move-line-down)
;;
;;; ChangeLog
;; 2006-12-08  GNU Emacs compat
;;
;;; Thanks
;; Christop Conrad for GNU Emacs suggestions
;;
;;; Code:
(defgroup swiss-move nil
  "Swiss movement moving with decreasing stepsizes."
  :tag "SwissMove"
  :link '(url-link :tag "Home Page"
                   "http://www.skamphausen.de/software/skamacs/")
  :link '(emacs-commentary-link
          :tag "Commentary in swiss-move.el" "swiss-move.el")
  :prefix "swiss-move-"
  :group 'environment
  :group 'extensions
  :group 'convenience)


(defcustom swiss-move-line-percent 50
  "*The percentage that is used to skip.
If set to 50 (the default) one swiss line move down from the beginning
of the buffer would bring you to the middle of it (50% ...), the next
jump to approximately 75%."
  :type 'number
  :group 'swiss-move)


(defvar swiss-move-saved-line nil
  "Used to store the last line swiss move jumped to.
This is important when changing the direction so that in that case the
stepsize is decreased, too.")

(defvar swiss-move-running nil
  "Used to track whether swiss movement is active or not.")

(if (featurep 'xemacs)
    (progn ;; XEmacs code:
      (defun swiss-move-line-number (&optional at-point)
    (line-number at-point)))
  (progn ;; GNU Emacs code
    (defun swiss-move-line-number (&optional at-point)
      (save-restriction
    (widen)
    (count-lines (point-min) (or at-point (point)))))))


(defun swiss-move-line-up ()
  "Swiss move upwards.
See `swiss-move-line' for details."
  (interactive)
  (swiss-move-line -1))

(defun swiss-move-line-down ()
  "Swiss move downwards.
See `swiss-move-line' for details."
  (interactive)
  (swiss-move-line 1))

(defun swiss-move-line (direction &optional percentage)
  "Move in decreasing numbers of lines.
Starting from current point this calculates a large jump using the
given DIRECTION.  DIRECTION.ist given as negativ number for upward and
positive number for downward movement.
Subsequent jumps will be smaller.  This works for
changing directions, too.  Thus you can close in on to your target.
Like this:


Start  press1 press2 press3
TOP
->.... ...... ...... ......
...... ...... ...... ......
...... ...... ...... ......
...... ...... ...... ......
...... ->.... ...... ......
...... ...... ...... ......
...... ...... ...... ->....
...... ...... ->.... ......
...... ...... ...... ......
...... ...... ...... ......
BOT

You might also consider the guessing a random number between 0 and
100. From you initial position 0 you jump forward half the distance to
50.  Then your quiz-master tells you that the random number is larger.
You take another jump forward, using half the last stepsize to 75.
This time your guess is too high, so take a step back, again taking
half of the last stepsize to 63.  And so on.

You'd better test it.
"
  (interactive)
  (or percentage (setq percentage swiss-move-line-percent))
  (let* ((currli (swiss-move-line-number))
         (otherli (cond
                   ((> direction 0)
                    (if (<= (or swiss-move-saved-line currli) currli)
                        (swiss-move-line-number (point-max))
                      swiss-move-saved-line))
                   ((< direction 0)
                    (if (>= (or swiss-move-saved-line currli) currli)
                        (swiss-move-line-number (point-min))
                      swiss-move-saved-line))
                   (t 0)))
         (skipli (floor (* (- otherli currli) 0.01 percentage))))
    ;;(message "from%d step%d save%s" currli skipli swiss-move-saved-line)
    (setq swiss-move-saved-line currli)
    (forward-line skipli))
  (swiss-move-hook-function))

(defun swiss-move-hook-function ()
  "Handle the swiss movement hook.
Install and remove ourself on `pre-command-hook' and control the swiss
movement active state."
  (if (and (not (or
                 (eq last-command 'swiss-move-line-up)
                 (eq last-command 'swiss-move-line-down)))
           (not (null swiss-move-running))
           (memq 'swiss-move-hook-function pre-command-hook))
      (progn
        (remove-hook 'pre-command-hook 'swiss-move-hook-function)
        (setq swiss-move-running nil)
        (setq swiss-move-saved-line nil))
    (unless (memq 'swiss-move-hook-function pre-command-hook)
      (setq swiss-move-running t)
      (add-hook 'pre-command-hook 'swiss-move-hook-function))))

;;; swiss-move.el ends here

(global-set-key (kbd "M-v") 'swiss-move-line-up)
(global-set-key (kbd "C-v") 'swiss-move-line-down)
